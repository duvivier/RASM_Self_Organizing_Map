;***************************************
; PROGRAM DESCRIPTION: This script plots node or group
;                      average variables
; INPUT DATA: WRF or POP node or group average files
; OUTPUT DATA: Panel plot of node or group averages
; CREATOR: Alice DuVivier - January 2015
;***************************************
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/shea_util.ncl"
;***************************************
begin
; MANUAL INPUTS - for testing purposes
; ******************************************************
  type = "node"
  varcode = "HMXL"    ;options: "WS" "BUOY" "HMXL"
  lag = 3      ; 1 day or 3 days
  nx = "4"
  ny = "3"
  mon = "DJF"   ; if passed as doing monthly average or not  
  per_s = "1"
  per_l = "3p"
; to skip around to sections search for "NEXT"
; ******************************************************
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; BEGIN SCRIPT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
print("Processing all plots for "+varcode)

;;;;;;;;;;
; Variable details
;;;;;;;;;;
if (varcode.eq."WS")then
  vartitle = "eta0 wind speed: average from start day to 3 days after start"
  invar = "wspd_lag"
  cmaptype = "wind"
  vartype = "4d"
  level = lag
  tag = "wrf"
end if
if (varcode.eq."BUOY")then
  vartitle = "buoyancy flux: average from start day to 3 days after start"
  invar = "Buoy_lag"
  cmaptype = "buoy_diff"
  vartype = "4d"
  level = lag
  tag = "pop"
end if
if (varcode.eq."HMXL")then
  vartitle = "change in mixed layer depth - 3 days after start"
  invar = "HMXL_diff"
  cmaptype = "mld_diff"
  vartype = "3d"
  level = lag
  tag = "pop"
end if

;;;;;;;;;;
; Contour intervals
;;;;;;;;;;
; NEXT
if (cmaptype.eq."wind") then
  cmin               = 0.0
  cmax               = 18.0
  clev               = 1.0
  stride             = 1                  
  plotlines          = False              
  spreadstart        = 2    ;2                  
  spreadend          = 129  ;18                 
  colormap           = "MPL_BuPu"  ;"SOM_wind_table_mod"
  lim_max            = 15.0
end if
if (cmaptype .eq."buoy_diff")then
  cmin               = 0.0
  cmax               = 15.0
  clev               = 1.0
  stride             = 2.0
  plotlines           = False
  spreadstart        = 125
  spreadend          = 250
  colormap           = "BlueYellowRed_mod"
  lim_max            = 15.0
end if
if (cmaptype .eq."mld_diff")then
  cmin               = -300.0
  cmax               = 300.0
  clev               = 30.0
  stride             = 2.0
  plotlines           = False
  spreadstart        = 2
  spreadend          = 250
  colormap           = "BlueYellowRed_mod"
  lim_max            = 100.0
end if

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
; Load in data from average files
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; NEXT
; set data input directory and fin suffix
dir_main = "/vardar/data5/duvivier/SOM-RASM/"
if (type .eq. "node")then
  nx_node = stringtoint(nx)
  ny_node = stringtoint(ny)
  npan = nx_node*ny_node  
  dir_data = dir_main+"analysis/averages_ndays/"+nx+"x_"+ny+"y_"+type+"_avgs/"
end if
; set geography data paths
geofile_w = dir_main+"DATA/wrf/geo_em.50km.green.nc"
geofile_p = dir_main+"DATA/pop/pop_geofile.nc"

; set suffix based on if this is monthly sub data or not
suffix_s = "_month-"+mon+"_persist-"+per_s+"_199011_201003."+tag
suffix_wind_s = "_month-"+mon+"_persist-"+per_s+"_199011_201003.wrf"
suffix_ice_s = "_month-"+mon+"_persist-"+per_s+"_199011_201003.pop"
suffix_l = "_month-"+mon+"_persist-"+per_l+"_199011_201003."+tag
suffix_wind_l = "_month-"+mon+"_persist-"+per_l+"_199011_201003.wrf"
suffix_ice_l = "_month-"+mon+"_persist-"+per_l+"_199011_201003.pop"
; set outdir and fout names
outdir = "./"
fout   = "figure_8-d3-"+varcode

; Load in lat/lon info for WRF or POP
if (tag .eq. "wrf")then
  geo = addfile(geofile_w,"r")
  lat2d = geo->XLAT_M(0,:,0:66)
  lon2d = geo->XLONG_M(0,:,0:66)
end if
if (tag .eq. "pop")then
  geo = addfile(geofile_p,"r")
  lat2d = geo->TLAT
  lon2d = geo->TLONG
end if
dims = dimsizes(lat2d)      ; get lat/lon dimensions
n_sn = dims(0)              ; get south/north points
n_we = dims(1)              ; get west/east points
delete([/geo,dims/])

;;;;;;;;;;
; Load in actual data for analysis
;;;;;;;;;;
; NEXT
; set invar for averages only
invar1 = invar+"_avg"

; create all-node array for node average variable
var1_s     = new((/npan,n_sn,n_we/),"float")
var1_l     = new((/npan,n_sn,n_we/),"float")
;ndates   = new((/npan/),"integer")
fill = var1_s@_FillValue

; Load sea ice for masking
geo_p = addfile(geofile_p,"r")
lat_p = geo_p->TLAT
lon_p = geo_p->TLONG
dims = dimsizes(lat_p)
n_sn_p = dims(0)
n_we_p = dims(1)
seaice_s = new((/npan,n_sn_p,n_we_p/),"float")
seaice_l = new((/npan,n_sn_p,n_we_p/),"float")
insea = "ice_frac_avg"
delete([/geo_p,dims,n_sn_p,n_we_p/]) 

; Load wind vectors for overlay
geo_w = addfile(geofile_w,"r")
lat_w = geo_w->XLAT_M(0,:,0:66)
lon_w = geo_w->XLONG_M(0,:,0:66)
z_sfc = geo_w->HGT_M(0,:,0:66)
dims = dimsizes(lat_w)
n_sn_w = dims(0)
n_we_w = dims(1)
u_vect_s   = new((/npan,n_sn_w,n_we_w/),"float")
v_vect_s   = new((/npan,n_sn_w,n_we_w/),"float")
u_vect_l   = new((/npan,n_sn_w,n_we_w/),"float")
v_vect_l   = new((/npan,n_sn_w,n_we_w/),"float")
invar_u_vec = "u_tr_lag_avg"
invar_v_vec = "v_tr_lag_avg"
delete([/geo_w,dims,n_sn_w,n_we_w/])

  n = 0
  y = 0
  x = 0
  do y = 0, ny_node - 1
    do x = 0, nx_node - 1
      print("Loading node "+x+"x "+y+"y")
      prefix = "node_"+x+"x_"+y+"y"

      ; load "short" event information
      f1 = addfile(dir_data+prefix+suffix_s+".sfc.nc","r")
      f2 = addfile(dir_data+prefix+suffix_ice_s+".sfc.nc","r")
      f3 = addfile(dir_data+prefix+suffix_wind_s+".sfc.nc","r")

      ; load variable with the number of days we want to average over
      if (vartype .eq. "3d")then
         var1_s(n,:,:) = f1->$invar1$(level,:,:)
      end if
      if (vartype .eq. "4d")then
         dum = f1->$invar1$(0:level,:,:)
         dum1 = dim_avg_n_Wrap(dum,0)
         var1_s(n,:,:) = dum1
         delete([/dum, dum1/])
      end if

      ; load sea ice average for day 1 (assume it doesn't change too much.)
      seaice_s(n,:,:) = f2->$insea$(:,:)
      ; load wind vectors with number of days we want to average over
      dum = f3->$invar_u_vec$(0:level,:,:)
      dum1 = dim_avg_n_Wrap(dum,0)
      u_vect_s(n,:,:) = dum1
      delete([/dum, dum1/])
      dum = f3->$invar_v_vec$(0:level,:,:)
      dum1 = dim_avg_n_Wrap(dum,0)
      v_vect_s(n,:,:) = dum1
      delete([/dum, dum1/])

      delete([/f1,f2,f3/])

      ; load "long" event information
      f1 = addfile(dir_data+prefix+suffix_l+".sfc.nc","r")
      f2 = addfile(dir_data+prefix+suffix_ice_l+".sfc.nc","r")
      f3 = addfile(dir_data+prefix+suffix_wind_l+".sfc.nc","r")

      ; load variable with the number of days we want to average over
      if (vartype .eq. "3d")then
         var1_l(n,:,:) = f1->$invar1$(level,:,:)
      end if
      if (vartype .eq. "4d")then
         dum = f1->$invar1$(0:level,:,:)
         dum1 = dim_avg_n_Wrap(dum,0)
         var1_l(n,:,:) = dum1
         delete([/dum, dum1/])
      end if

      ; load sea ice average for day 1 (assume it doesn't change too much.)
      seaice_l(n,:,:) = f2->$insea$(:,:)
      ; load wind vectors with number of days we want to average over
      dum = f3->$invar_u_vec$(0:level,:,:)
      dum1 = dim_avg_n_Wrap(dum,0)
      u_vect_l(n,:,:) = dum1
      delete([/dum, dum1/])
      dum = f3->$invar_v_vec$(0:level,:,:)
      dum1 = dim_avg_n_Wrap(dum,0)
      v_vect_l(n,:,:) = dum1
      delete([/dum, dum1/])

      delete([/prefix,f1,f2,f3/])
      n = n+1
    end do
  end do
  delete(n)

;;;;;;;;;;
; Change units as necessary
;;;;;;;;;;
; NEXT
if (cmaptype .eq. "buoy_diff")then
  var1_s = var1_s*10.0^8
  var1_s@units = "x10^-8 m2 s-3"
  var1_l = var1_l*10.0^8
  var1_l@units = "x10^-8 m2 s-3"
end if
if (cmaptype .eq. "mld_diff")then
  var1_s = var1_s/100.
  var1_s@units = "m"
  var1_l = var1_l/100.
  var1_l@units = "m"
end if

print("Loaded "+vartitle)
;;;;;;;;;;
; Mask terrain or sea ice
;;;;;;;;;;
; NEXT
print("Masking variable")
n = 0
; loop through each file
do n = 0, npan - 1
  ; mask terrain (wrf files)
  if (tag .eq. "wrf") then
    var1_s(n,:,:) = where(z_sfc .lt. 25.0, var1_s(n,:,:), var1_s@_FillValue)
    var1_l(n,:,:) = where(z_sfc .lt. 25.0, var1_l(n,:,:), var1_l@_FillValue)
  end if
  if (tag .eq. "pop") then
    var1_s(n,:,:) = where(seaice_s(n,:,:) .lt. 0.15, var1_s(n,:,:), var1_s@_FillValue)
    var1_l(n,:,:) = where(seaice_l(n,:,:) .lt. 0.15, var1_l(n,:,:), var1_l@_FillValue)
  end if
  u_vect_s(n,:,:) = where(z_sfc .lt. 25.0, u_vect_s(n,:,:),u_vect_s@_FillValue)
  v_vect_s(n,:,:) = where(z_sfc .lt. 25.0, v_vect_s(n,:,:),v_vect_s@_FillValue)
  u_vect_l(n,:,:) = where(z_sfc .lt. 25.0, u_vect_l(n,:,:),u_vect_l@_FillValue)
  v_vect_l(n,:,:) = where(z_sfc .lt. 25.0, v_vect_l(n,:,:),v_vect_l@_FillValue)
end do
delete(n)

; assign lat/lon info
var1_s@lat2d = lat2d
var1_s@lon2d = lon2d
seaice_s@lat2d = lat_p
seaice_s@lon2d = lon_p
u_vect_s@lat2d = lat_w
u_vect_s@lon2d = lon_w
v_vect_s@lat2d = lat_w
v_vect_s@lon2d = lon_w
var1_l@lat2d = lat2d
var1_l@lon2d = lon2d
seaice_l@lat2d = lat_p
seaice_l@lon2d = lon_p
u_vect_l@lat2d = lat_w
u_vect_l@lon2d = lon_w
v_vect_l@lat2d = lat_w
v_vect_l@lon2d = lon_w

print(varcode+" var min: "+min(var1_s)+"  max: "+max(var1_s))
print(varcode+" var min: "+min(var1_l)+"  max: "+max(var1_l))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
; Plotting
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; NEXT
print("Processing all plots for "+varcode)
;;;;;;;;;;
; Set resources for normal plots
;;;;;;;;;;
res                             = True              ; plot mods desired
res@gsnRightString              = ""
res@gsnLeftString               = ""               ; Set this in loop
res@gsnFrame                    = False             ; Do not draw plot 
res@gsnDraw                     = False             ; Do not advance fr
res@gsnAddCyclic                = False             ; regional - not cyclic
res@gsnMaximize                 = True              ; Make it as big as possible
res@mpProjection                = "Stereographic"   ; map projection
res@mpCenterLonF                = -35. ;-30.             ; set the center lat/lon
res@mpCenterLatF                = 63.               
res@mpGridAndLimbOn             = False            ; Turn on lat/lon grid
;res@mpGridLineDashPattern       = 2                ; Change dash pattern
res@mpFillOn                    = True             ; Enables map fill
res@mpGeophysicalLineColor      = "black"          ; Color of continents
res@mpGeophysicalLineThicknessF = 2.5              ; Make continents thinner
if (tag .eq. "pop")then
  res@trGridType                  = "TriangularMesh"
end if 

res@mpLimitMode                 = "LatLon"	     ; how to zoom in on map
res@mpMinLatF                   = 56.              ; set the minimum latitude 
res@mpMaxLatF                   = 70.              ; set the maximum latitude 
res@mpMinLonF                   = -54.0            ; set the minimum longitude 
res@mpMaxLonF                   = -20.0            ; set the maximum longitude 

; Resources for contours
res@cnFillMode                  = "AreaFill"
res@cnFillOn                    = True              ; turn on filled contours
res@gsnSpreadColors             = True              ; use full colormap
res@cnLevelSelectionMode        = "ManualLevels"    ; set manual contour
res@cnLinesOn                   = plotlines         ; contour lines specified above
res@cnLineLabelsOn              = plotlines       
res@cnMissingValFillColor       = "gray85"          ; missing value color
res@cnMissingValFillPattern     = 0                 ; fill in
res@cnMissingValPerimOn         = True
res@cnMissingValPerimColor      = "black"
res@tiMainFontHeightF           = 0.013
res@lbLabelBarOn                = False
res@cnInfoLabelOn               = False

res@gsnSpreadColorStart        = spreadstart      ; start at color specified for each variable above
res@gsnSpreadColorEnd          = spreadend        ; end at color specified above
res@cnMinLevelValF             = cmin             ; set min contour level specified above
res@cnMaxLevelValF             = cmax             ; set max contour level specified above
res@cnLevelSpacingF            = clev             ; set contour spacing specified above
;res@lbLabelStride              = stride

; resources for vector plots if it's a wind plot
vcres                            = True ;res
vcres@gsnDraw                    = False
vcres@gsnFrame                   = False
vcres@vcGlyphStyle               = "LineArrow"
vcres@vcRefMagnitudeF            = 10.0
vcres@vcRefLengthF               = 0.05
vcres@vcMonoLineArrowColor       = True
vcres@vcLineArrowColor           = "black"
vcres@vcMinDistanceF             = 0.07
vcres@vcRefAnnoOn                = True ;False
vcres@gsnLeftString              = ""
vcres@gsnRightString             = ""
vcres@vcRefAnnoFontHeightF       = 0.02
vcres@vcRefAnnoString1           = "10 m/s"
vcres@vcRefAnnoString2           = ""
vcres@vcRefAnnoOrthogonalPosF    = -0.15
;vcres@gsnScalarContour           = True
;vcres@cnInfoLabelOn              = False
;vcres@mpOutlineOn                = True
;vcres@mpOutlineBoundarySets      = "AllBoundaries" 

; set resources for contour maxima overlay
res_max                            = True               ; reset options
res_max@gsnLeftString              = ""
res_max@gsnRightString             = ""
res_max@gsnFrame                   = False             ; Do not draw plot 
res_max@gsnDraw                    = False             ; Do not advance fr
res_max@cnInfoLabelOn              = False             ; Turn off info on contours at bottom
res_max@cnFillOn                   = False             ; turn on filled contours
res_max@cnLevelSelectionMode       = "ManualLevels"    ; set manual contour
if (varcode.eq."HMXL")then
   res_max@cnMinLevelValF             = -300.                ; set min contour level specified above
   res_max@cnMaxLevelValF             = lim_max           ; set max contour level specified above
   res_max@cnLevelSpacingF            = 300.0+lim_max           ; set contour spacing specified above
else
   res_max@cnMinLevelValF             = 0.                ; set min contour level specified above
   res_max@cnMaxLevelValF             = lim_max           ; set max contour level specified above
   res_max@cnLevelSpacingF            = lim_max           ; set contour spacing specified above
end if
res_max@cnLinesOn                  = True              ; contour lines specified above
res_max@cnLineLabelsOn             = False             ; line labels on specified above
if (varcode.eq."WS")then
   res_max@cnLineColor                = "grey"
else
   res_max@cnLineColor                = "black"
end if
res_max@cnLineDashPattern          = 2                 ; small dashes
res_max@cnLineThicknessF           = 1.5
res_max@tiXAxisOn                  = False
res_max@tiYAxisOn                  = False

; set resources for sea ice overlay
res_ice                            = True               ; reset options
res_ice@gsnLeftString              = ""
res_ice@gsnRightString             = ""
res_ice@gsnFrame                   = False             ; Do not draw plot 
res_ice@gsnDraw                    = False             ; Do not advance fr
res_ice@cnInfoLabelOn              = False             ; Turn off info on contours at bottom
res_ice@cnFillOn                   = False             ; turn on filled contours
res_ice@cnLevelSelectionMode       = "ManualLevels"    ; set manual contour
res_ice@cnMinLevelValF             = 0.                ; set min contour level specified above
res_ice@cnMaxLevelValF             = 0.15              ; set max contour level specified above
res_ice@cnLevelSpacingF            = 0.15              ; set contour spacing specified above
res_ice@cnLinesOn                  = True              ; contour lines specified above
res_ice@cnLineLabelsOn             = False             ; line labels on specified above
res_ice@cnLineColor                = "black"
res_ice@cnLineThicknessF           = 3.0
res_ice@tiXAxisOn                  = False
res_ice@tiYAxisOn                  = False
res_ice@trGridType                 = "TriangularMesh"

; resources for final panel plot
res_pan                            = True
res_pan@txString                   = "Short vs. long events for 3 days after start"
res_pan@gsnPaperOrientation        = "landscape"      ; landscape mode
res_pan@gsnMaximize                = True	      ; fill the page
res_pan@gsnPanelLabelBar           = True
res_pan@lbTitleString              = var1_s@units
res_pan@lbLabelStride              = stride

; label lat/lon
lat_t1 = 69.0
lon_t1 = -40.5
lat_t2 = 67.0
lon_t2 = -40.5
txres                             = True
txres@txFontHeightF               = 0.06
txres@txFontThicknessF            = 2.0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
; Plot 1 - Winter mean 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
wks = gsn_open_wks("ps" ,fout)
additional_colors = (/"gray85", "forestgreen", "yellow"/)
gsn_merge_colormaps(wks,colormap,additional_colors)
plot = new(8, graphic)
dum1 = new(8, graphic)

print("Plotting short and long events for "+varcode)
;;;;;;;;
; Node 0,0 short and long
;;;;;;;;
    ; short event
    cplot = gsn_csm_contour_map(wks,var1_s(0,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_s(0,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_s(0,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_s(0,:,:),v_vect_s(0,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(0) = gsn_add_text(wks,cplot,"(0,0)~C~short", lon_t1, lat_t1, txres)
    plot(0) = cplot
    ; long event
    cplot = gsn_csm_contour_map(wks,var1_l(0,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_l(0,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_l(0,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_l(0,:,:),v_vect_l(0,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(1) = gsn_add_text(wks,cplot,"(0,0)~C~long", lon_t1, lat_t1, txres)
    plot(1) = cplot

;;;;;;;;
; Node 3,0 short and long
;;;;;;;;
    ; short event
    cplot = gsn_csm_contour_map(wks,var1_s(3,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_s(3,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_s(3,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_s(3,:,:),v_vect_s(3,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(2) = gsn_add_text(wks,cplot,"(3,0)~C~short", lon_t1, lat_t1, txres)
    plot(2) = cplot
    ; long event
    cplot = gsn_csm_contour_map(wks,var1_l(3,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_l(3,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_l(3,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_l(3,:,:),v_vect_l(3,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(3) = gsn_add_text(wks,cplot,"(3,0)~C~long", lon_t1, lat_t1, txres)
    plot(3) = cplot

;;;;;;;;
; Node 0,2 short and long
;;;;;;;;
    ; short event
    cplot = gsn_csm_contour_map(wks,var1_s(8,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_s(8,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_s(8,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_s(8,:,:),v_vect_s(8,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(4) = gsn_add_text(wks,cplot,"(0,2)~C~short", lon_t1, lat_t1, txres)
    plot(4) = cplot
    ; long event
    cplot = gsn_csm_contour_map(wks,var1_l(8,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_l(8,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_l(8,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_l(8,:,:),v_vect_l(8,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(5) = gsn_add_text(wks,cplot,"(0,2)~C~long", lon_t1, lat_t1, txres)
    plot(5) = cplot

;;;;;;;;
; Node 3,2 short and long
;;;;;;;;
    ; short event
    cplot = gsn_csm_contour_map(wks,var1_s(11,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_s(11,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_s(11,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_s(11,:,:),v_vect_s(11,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(6) = gsn_add_text(wks,cplot,"(3,2)~C~short", lon_t1, lat_t1, txres)
    plot(6) = cplot
    ; long event
    cplot = gsn_csm_contour_map(wks,var1_l(11,:,:),res)
    iplot = gsn_csm_contour(wks,seaice_l(11,:,:),res_ice)
    overlay(cplot,iplot)
    mplot = gsn_csm_contour(wks,var1_l(11,:,:),res_max)
    overlay(cplot,mplot)
    if(varcode .eq. "WS")then
       vplot = gsn_csm_vector(wks,u_vect_l(11,:,:),v_vect_l(11,:,:),vcres)
       overlay(cplot,vplot)
    end if
    dum1(7) = gsn_add_text(wks,cplot,"(3,2)~C~long", lon_t1, lat_t1, txres)
    plot(7) = cplot

  ; Panel the group plots
  res_pan@gsnPanelYWhiteSpacePercent = 2                ; Add white space b/w plots.
  res_pan@gsnPanelXWhiteSpacePercent = 2                ; Add white space b/w plots.
  res_pan@gsnPanelScalePlotIndex     = 1
  res_pan@lbOrientation              = "Horizontal"
  res_pan@lbTitlePosition            = "right"
  res_pan@lbTitleDirection           = "Across"
  res_pan@lbTitleJust                = "CenterLeft"
  res_pan@lbTitleFontHeightF         = 0.015 ;0.0075
  res_pan@lbLabelFontHeightF         = 0.0095
  res_pan@pmLabelBarWidthF           = 0.975
  res_pan@pmLabelBarHeightF          = 0.075
  res_pan@txFontHeightF              = 0.015
  gsn_panel(wks,plot,(/2,4/),res_pan)      ; create final panel plot

; Finally - make plot
print("converting plot to png")
delete(wks)
system("convert -trim -border 10 -bordercolor white -density 300 "+  \
         "-rotate -90 -trim -border 10 "+fout+".ps "+fout+".png")
print("Completed all plots for "+varcode+". Good job!")
;;;;;;;;;;;;;;;;;;;;;; END script
end
